plugins {
  id 'maven'
  id 'maven-publish'
  id 'java-library'

  id "com.gradle.plugin-publish" version "0.10.1"
  id "java-gradle-plugin"

  id 'checkstyle'
  id 'com.diffplug.gradle.spotless' version '3.24.0'
  id 'net.ltgt.errorprone' version '0.8.1'
  id 'pmd'
  id 'com.github.ben-manes.versions' version '0.21.0'

  id 'edu.wpi.first.GradleRIO' version '2019.4.1'
}

group = 'com.github.HillelCoates'

repositories { mavenCentral() }

dependencies {
  errorprone('com.google.errorprone:error_prone_core:2.3.3')

  wpi.deps.wpilibJni().forEach { nativeZip(it) }
  wpi.deps.wpilibDesktopJni().forEach { nativeDesktopZip(it) }
  wpi.deps.wpilibJars().forEach { compile(it) }

  implementation 'com.google.guava:guava:28.0-jre'
  implementation 'com.google.code.gson:gson:2.8.5'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
  testImplementation 'org.assertj:assertj-core:3.13.1'
}

compileJava { dependsOn 'spotlessApply' }

test {
  useJUnitPlatform { excludeTags 'slow' }
  testLogging.showStandardStreams = true
}

spotless {
  java { googleJavaFormat('1.7') }
  groovyGradle {
    greclipse()
    indentWithSpaces(2)
  }
}

checkstyle {
  toolVersion = '8.21'
  config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
  configProperties = [ 'suppressionFile': project(':').file('config/checkstyle/suppressions.xml') ]
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    freezylib(MavenPublication) {
      from components.java
      artifact sourceJar
      artifact javadocJar
    }
  }
}
